// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        buildToolsVersion = "34.0.0"
        minSdkVersion = 21
        compileSdkVersion = 34
        targetSdkVersion = 34

        // We use NDK 23 which has both M1 support and is the side-by-side NDK version from AGP.
        ndkVersion = "23.1.7779620"
        kotlinVersion = "1.8.0"
    }
    configurations.classpath {
        resolutionStrategy {
            force 'org.bouncycastle:bcprov-jdk18on:1.76'
        }
    }
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath("com.android.tools.build:gradle:8.1.0")
        classpath("com.facebook.react:react-native-gradle-plugin")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion")
    }
}

allprojects {
    configurations.all {
        resolutionStrategy {
            force 'androidx.appcompat:appcompat:1.6.1'
            force 'androidx.appcompat:appcompat-resources:1.6.1'
            force 'androidx.core:core:1.10.1'
            force 'androidx.core:core-ktx:1.10.1'
            force 'androidx.annotation:annotation-experimental:1.3.0'
            force 'org.bouncycastle:bcprov-jdk18on:1.76'
        }
    }
    
    // Fix for AGP 8.x compatibility
    afterEvaluate { project ->
        if (project.hasProperty("android")) {
            project.android {
                // Set namespace if missing
                if (namespace == null || namespace.isEmpty()) {
                    namespace = project.group?.toString() ?: "com.${project.name.replace('-', '_')}"
                }
                // Enable BuildConfig for libraries that use it
                buildFeatures {
                    buildConfig true
                }
            }
        }
    }
}

subprojects { subproject ->
    subproject.buildscript {
        configurations.matching { it.name == "classpath" }.all {
            resolutionStrategy {
                force 'org.bouncycastle:bcprov-jdk18on:1.76'
            }
        }
    }
}

